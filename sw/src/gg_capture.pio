.define dw 20 ;Vsync
.define cl2  21; ;Hsync
.define clk 22 ;Pixel clock

;///////////////////////////////////////
;/////////////// WARNING ///////////////
;///////////////////////////////////////

;The timings involved in capturing data from the GG are quite finicky
;Modify these programs with caution, especially get_data

.program detect_vblank
.side_set 1 opt
.wrap_target
    wait 0 gpio dw
		wait 1 gpio dw
		irq set 0
.wrap

.program detect_hblank
.side_set 1 opt
.wrap_target
;OSR contains the number of scanlines to capture
		mov x, osr side 0
		wait 1 irq 0

loop_scanline:
    wait 1 gpio cl2 side 1

		set y, 4

wait_cycles:
		wait 0 gpio clk
		wait 1 gpio clk
		jmp y-- wait_cycles

		irq set 1
		wait 0 gpio cl2
		jmp x-- loop_scanline
.wrap

.program get_data
.side_set 1 opt
.wrap_target
;OSR -> Amount of pixels to capture in this scanline
	mov x, osr
	wait 1 irq 1

capture_pixels:
	set y, 2 side 0
inner_loop:
	wait 1 gpio clk
skip_start:
	wait 0 gpio clk

	wait 1 gpio clk
	in pins, 4
	wait 0 gpio clk

	jmp y-- inner_loop

	push

	jmp x-- capture_pixels
	nop side 1
.wrap
